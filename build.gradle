apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'jetty'

// Properties for the application plugin
mainClassName = 'com.jayway.gradledemo.OtherDemo'

sourceCompatibility = 1.7
targetCompatibility = 1.7

project.group = 'com.jayway.gradledemo'
version = '0.1-SNAPSHOT'


jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Quickstart', 'Implementation-Version': version
    }
}


repositories {
    mavenCentral()
}


dependencies {
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    compile 'javax.servlet:servlet-api:2.5'
    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile group: 'com.jayway.restassured', name: 'rest-assured', version: '1.8.0'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
}


test {
    exclude '**/IT*.*', '**/*IT.*', '**/*ITCase.*'
}


[jettyRun, jettyRunWar, jettyStop]*.stopPort = 9999
[jettyRun, jettyRunWar, jettyStop]*.stopKey = 'stop'

task integrationTest(type: Test, dependsOn: test) {
    description = 'Runs the integration tests.'
    include '**/IT*.*', '**/*IT.*', '**/*ITCase.*'
    doFirst {
        jettyRun.daemon = true
        jettyRun.execute()
    }
    doLast {
        jettyStop.execute()
    }
}


uploadArchives {
    repositories {
        flatDir {
            dirs 'repos'
        }
    }
}


task wrapper(type: Wrapper) {
    description = 'Creates a Gradle Wrapper.'
    gradleVersion = '1.5'
}